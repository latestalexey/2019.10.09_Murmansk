
#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
	
	ЭтаФорма.ВНовыйДокумент = Истина;
	ИмяТабличнойЧасти = _ЛокКонтекст.ЛокЯдро_ПолучитьИмяТабличнойЧастиДокумента(ТипДокумента1С);
	ЗаполнитьТаблицуДокументы1С();
	УстановитьДоступностьТаблицыДокументы1С();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

Процедура Документы1СВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	ТекущиеДанные = ЭлементыФормы.Документы1С.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;		
	
КонецПроцедуры

Процедура КоманднаяПанельПодвалФормы(Кнопка)
	
	ОбработатьВыборДокументаДляЗагрузки(ЭлементыФормы.Документы1С.ТекущиеДанные);	
	
КонецПроцедуры

Процедура ВНовыйДокументПриИзменении(Элемент)
	
	УстановитьДоступностьТаблицыДокументы1С();
	
КонецПроцедуры

Процедура Документы1СПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ОформлениеСтроки.Ячейки.Название.Значение = СокрЛП(ОформлениеСтроки.ДанныеСтроки.Ссылка); 		
		
		Если ЗначениеЗаполнено(ИмяТабличнойЧасти) Тогда
			КоличествоТоваров = ОформлениеСтроки.ДанныеСтроки.Ссылка[ИмяТабличнойЧасти].Количество();
			Если КоличествоТоваров = 0 Тогда
				ОформлениеСтроки.Ячейки.КоличествоСтрок.Значение = "";	
			Иначе
				ОформлениеСтроки.Ячейки.КоличествоСтрок.Значение = СокрЛП(КоличествоТоваров);	
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТаблицуДокументы1С()

	МассивТиповДокументов = Новый Массив;
	МассивТиповДокументов.Добавить(Тип("ДокументСписок." + ТипДокумента1С));
	
	ЭлементыФормы.Документы1С.ТипЗначения = Новый ОписаниеТипов(МассивТиповДокументов);			
	ЭлементыФормы.Документы1С.СоздатьКолонки();
	
	КолонкаДляУдаления = ЭлементыФормы.Документы1С.Колонки.Проведен;
	ЭлементыФормы.Документы1С.Колонки.Удалить(КолонкаДляУдаления);
	
	КолонкаДляУдаления = ЭлементыФормы.Документы1С.Колонки.Картинка;
	ЭлементыФормы.Документы1С.Колонки.Удалить(КолонкаДляУдаления);
	
	ЭлементыФормы.Документы1С.Колонки.Добавить("Название");			
	ЭлементыФормы.Документы1С.Колонки.Название.УстановитьЭлементУправления(Тип("ПолеВвода"));
	ЭлементыФормы.Документы1С.Колонки.Название.ЭлементУправления.ТипЗначения = Новый ОписаниеТипов("Строка");
	
	ЭлементыФормы.Документы1С.Колонки.Добавить("КоличествоСтрок", "Кол-во строк");			
	ЭлементыФормы.Документы1С.Колонки.Название.УстановитьЭлементУправления(Тип("ПолеВвода"));
	ЭлементыФормы.Документы1С.Колонки.Название.ЭлементУправления.ТипЗначения = Новый ОписаниеТипов("Строка");	
	
	ЭлементыФормы.Документы1С.Колонки.Дата.Ширина 				= 15;
	ЭлементыФормы.Документы1С.Колонки.Номер.Ширина 				= 15;
	ЭлементыФормы.Документы1С.Колонки.Название.Ширина 			= 60;
	ЭлементыФормы.Документы1С.Колонки.КоличествоСтрок.Ширина 	= 10;	
	
	ЭлементыФормы.Документы1С.ОбновитьСтроки();

КонецПроцедуры

Процедура ОбработатьВыборДокументаДляЗагрузки(ВыбраннаяСтрока)
	
	РезультатВыбора = Неопределено;
	
	Если ЭтаФорма.ВНовыйДокумент Тогда
		РезультатВыбора = Документы[ТипДокумента1С].ПустаяСсылка();
	Иначе
		Если ВыбраннаяСтрока <> Неопределено Тогда
			РезультатВыбора = ВыбраннаяСтрока.Ссылка;
		Иначе
			Сообщить("Пожалуйста, выберите документ для загрузки данных из ТСД");
			Возврат;
		КонецЕсли;	
	КонецЕсли;	

	ЭтаФорма.Закрыть(РезультатВыбора);
	
КонецПроцедуры

Процедура УстановитьДоступностьТаблицыДокументы1С()

	ЭлементыФормы.Документы1С.Доступность 					= НЕ ЭтаФорма.ВНовыйДокумент;
	ЭлементыФормы.КоманднаяПанельДокументы1С.Доступность 	= НЕ ЭтаФорма.ВНовыйДокумент;

КонецПроцедуры

#КонецОбласти 
 