#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
	
	ОбновитьТаблицуПроизвольныхОтборовДокументов(ID_ПроизвольногоОтбора);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПанельДействийФормы

Процедура ОсновныеДействияСохранитьВФайл(Кнопка)
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("ТаблицаПроизвольныхОтборовДокументов", ТаблицаПроизвольныхОтборовДокументов);
	
	_ЛокКонтекст.ЛокЯдро_СохранитьНастройкиВФайл(СтруктураНастройки, "ПроизвольныеОтборыДокументов");
	
КонецПроцедуры

Процедура ОсновныеДействияЗагрузитьИзФайла(Кнопка)
	
	СтруктураНастроек = _ЛокКонтекст.ЛокЯдро_ПолучитьНастройкиИзФайла("ПроизвольныеОтборыДокументов");
	
	Если СтруктураНастроек <> Неопределено Тогда
		
		_ЛокКонтекст.ЛокЯдро_ЗаполнитьФормуЗначениямиИзСтруктуры(ЭтаФорма, СтруктураНастроек);
		_ЛокКонтекст.ЛокЯдро_СохранитьНастройкуВБазеSMARTS("ПроизвольныеОтборыДокументов", ТаблицаПроизвольныхОтборовДокументов);
		_ЛокКонтекст.ЛокЯдро_ЗаписатьВКэшНастройкуSMARTS("ПроизвольныеОтборыДокументов", ТаблицаПроизвольныхОтборовДокументов);
		
		ОбновитьТаблицуПроизвольныхОтборовДокументов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеМеханизмы

Процедура ОткрытьФормуРедактированияНастройки(ЭтоНоваяНастройка, ЭтоКопирование = Ложь)
	
	ТекущаяСтрока = ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.ТекущаяСтрока;
	
	ФормаРедактирования = ПолучитьФорму("ФормаНастройки_ПроизвольныйОтборДокументов", ЭтаФорма, ЭтаФорма);
	ФормаРедактирования._ЛокКонтекст = _ЛокКонтекст;	
	ФормаРедактирования.ЭтоНоваяНастройка = ЭтоНоваяНастройка;
	
	Если НЕ ЭтоНоваяНастройка ИЛИ ЭтоКопирование Тогда		
		ЗаполнитьЗначенияСвойств(ФормаРедактирования, ТекущаяСтрока);
	КонецЕсли;
	
	ID_РедактируемойНастройки = ФормаРедактирования.ОткрытьМодально();
	Если ЗначениеЗаполнено(ID_РедактируемойНастройки) Тогда
		ОбновитьТаблицуПроизвольныхОтборовДокументов(ID_РедактируемойНастройки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьТаблицуПроизвольныхОтборовДокументов(ID_ТекущейСтроки = "")
	
	ТаблицаПроизвольныхОтборовДокументов = _ЛокКонтекст.ЛокЯдро_ПолучитьИзКэшаНастройкуSMARTS("ПроизвольныеОтборыДокументов");
	
	Если ЗначениеЗаполнено(ID_ТекущейСтроки) Тогда
		НайденнаяСтрока = ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.Значение.Найти(ID_ТекущейСтроки, "ID_ПроизвольногоОтбора");
		Если НайденнаяСтрока <> Неопределено Тогда
			ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.ТекущаяСтрока = НайденнаяСтрока;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьНастройки()
	
	ТаблицаДляСохранения = ТаблицаПроизвольныхОтборовДокументов.Скопировать();
	_ЛокКонтекст.ЛокЯдро_СохранитьНастройкуВБазеSMARTS("ПроизвольныеОтборыДокументов", ТаблицаДляСохранения);	
	_ЛокКонтекст.ЛокЯдро_ЗаписатьВКэшНастройкуSMARTS("ПроизвольныеОтборыДокументов", ТаблицаДляСохранения);		

КонецПроцедуры	
	
Процедура ЗакрытьФормуСВыборомЭлемента()
	
	Если ЗначениеЗаполнено(ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.ТекущаяСтрока) Тогда
		Закрыть(ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.ТекущаяСтрока.ID_ПроизвольногоОтбора);
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоля_ТаблицаПроизвольныхОтборовДокументов

Процедура ТаблицаПроизвольныхОтборовДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗакрытьФормуСВыборомЭлемента();
КонецПроцедуры

Процедура ТаблицаПроизвольныхОтборовДокументовПередНачаломДобавления(Элемент, Отказ, Копирование)
	ОткрытьФормуРедактированияНастройки(Истина, Копирование);
	Отказ = Истина;
КонецПроцедуры

Процедура ТаблицаПроизвольныхОтборовДокументовПередНачаломИзменения(Элемент, Отказ)
	ОткрытьФормуРедактированияНастройки(Ложь);
КонецПроцедуры

#КонецОбласти

Процедура КомПанельТаблицыВыбрать(Кнопка)
	ЗакрытьФормуСВыборомЭлемента();
КонецПроцедуры

Процедура ТаблицаПроизвольныхОтборовДокументовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	Если Вопрос("Вы действительно хотите удалить произвольный отбор?", РежимДиалогаВопрос.ДаНетОтмена, , , "Внимание") = КодВозвратаДиалога.Да Тогда			
		ТаблицаПроизвольныхОтборовДокументов.Удалить(ЭлементыФормы.ТаблицаПроизвольныхОтборовДокументов.ТекущаяСтрока);
		СохранитьНастройки();
	КонецЕсли; 
КонецПроцедуры
