
#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
		
	СписокТабличныхЧастей1С = ПолучитьСписокТабличныхЧастей1С();
	СписокТабличныхЧастейMS = ПолучитьСписокТабличныхЧастейMS();
	
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора 	= СписокТабличныхЧастейMS; 		
	ЭлементыФормы.ТабличныеЧасти.Колонки.НастройкаТЧСтрокой.ЭлементУправления.СписокВыбора 			= СписокТабличныхЧастей1С;
	
	ЭлементыФормы.СвязиТабличныхЧастей.Колонки.ГлавнаяТаблица.ЭлементУправления.СписокВыбора 				= СписокТабличныхЧастей1С; 	
	ЭлементыФормы.СвязиТабличныхЧастей.Колонки.ПодчиненнаяТаблица.ЭлементУправления.СписокВыбора 			= СписокТабличныхЧастей1С; 		
	ЭлементыФормы.СвязиТабличныхЧастей.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора 	= СписокТабличныхЧастейMS; 		
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийОсновныеДействия

Процедура ОсновныеДействияСохранитьИЗакрыть(Кнопка)
	
	Результат = Новый Структура;
	Результат.Вставить("ТабличныеЧасти", 		ТабличныеЧасти.Скопировать());
	Результат.Вставить("СвязиТабличныхЧастей", 	СвязиТабличныхЧастей.Скопировать());
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоля_ТабличныеЧасти

Процедура ТабличныеЧастиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВРег(СокрЛП(Колонка.Имя)) <> ВРег(СокрЛП("Настроить")) Тогда		
		Возврат;			
	КонецЕсли; 
	
	ТекущаяСтрока = ЭлементыФормы.ТабличныеЧасти.ТекущаяСтрока;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.НастройкаТЧСтрокой) Тогда
		Предупреждение("Не выбрана табличная часть документа 1С");
		Возврат;
	КонецЕсли; 
	
	ФормаРедактирования = ПолучитьФорму("ФормаНастройки_ПравилаВыгрузкиПолей", ЭтаФорма, ЭтаФорма);
	ФормаРедактирования._ЛокКонтекст  = _ЛокКонтекст;
	ФормаРедактирования.КлючНастройки = "Выгрузка_ТабличнаяЧастьДокумента";	
	ФормаРедактирования.ТипДокумента1С                   = ТипДокумента1С;
	ФормаРедактирования.ТипДокументаMS                   = ТипДокументаMS;
	ФормаРедактирования.ИмяТабличнойЧастиMS              = ТабличнаяЧастьДокументаMS;
	ФормаРедактирования.ИмяТабличнойЧасти1С              = ТекущаяСтрока.НастройкаТЧСтрокой;
	ФормаРедактирования.ТекстЗапросаВыгрузкиНаТСД        = ТекущаяСтрока.ТекстЗапросаВыгрузкиНаТСД;
	
	Если ТипЗнч(ТекущаяСтрока.ТабличнаяЧастьДокумента1С) = Тип("ТаблицаЗначений") Тогда
		ФормаРедактирования.ТаблицаРеквизитовИсходная   = ТекущаяСтрока.ТабличнаяЧастьДокумента1С.Скопировать();
	КонецЕсли;
	ФормаРедактирования.ТаблицаРеквизитовДополнительная  = ТаблицаРеквизитовДополнительная;
	
	РезультатРедактирования = ФормаРедактирования.ОткрытьМодально();
	
	Если РезультатРедактирования <> Неопределено Тогда
		ТекущаяСтрока.ТабличнаяЧастьДокумента1С = РезультатРедактирования.ТаблицаНастроек;		
		ТекущаяСтрока.ТекстЗапросаВыгрузкиНаТСД = РезультатРедактирования.ТекстЗапросаВыгрузкиНаТСД;		
		ТекущаяСтрока.Настроить					= "Настроено";
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабличныеЧастиПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		флНастроено = (ОформлениеСтроки.ДанныеСтроки.ТабличнаяЧастьДокумента1С.Количество() > 0);
		
		Если флНастроено Тогда
			ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроено";
		Иначе		
			ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроить";			
		КонецЕсли;
		
		ОформлениеСтроки.Ячейки.Настроить.ЦветТекста = ?(флНастроено, WebЦвета.Зеленый, WebЦвета.Красный);			
	
	КонецЦикла; 	
	
КонецПроцедуры

Процедура ТабличныеЧастиПередНачаломДобавления(Элемент, Отказ, Копирование)
		
	Отказ = Истина;
		
	НоваяСтрока = ТабличныеЧасти.Добавить();	
	НоваяСтрока.ТабличнаяЧастьДокументаMS = ТабличнаяЧастьДокументаMS;	
	
	Если Копирование Тогда
		
		ТекущаяСтрока = ЭлементыФормы.ТабличныеЧасти.ТекущаяСтрока;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока, , "ТабличнаяЧастьДокументаMS, ТабличнаяЧастьДокумента1С");
		НоваяСтрока.ТабличнаяЧастьДокумента1С = ТекущаяСтрока.ТабличнаяЧастьДокумента1С.Скопировать();
		
	КонецЕсли; 
	
	ЭлементыФормы.ТабличныеЧасти.ТекущаяСтрока = НоваяСтрока;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоля_СвязиТабличныхЧастей

Процедура СвязиТабличныхЧастейПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ОформлениеСтроки.Ячейки.УсловиеСравнения.Текст = "=";	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура СвязиТабличныхЧастейПолеГлавнойТаблицыНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.СвязиТабличныхЧастей.ТекущаяСтрока;	
	ЭлементыФормы.СвязиТабличныхЧастей.Колонки.ПолеГлавнойТаблицы.ЭлементУправления.СписокВыбора = СтруктураДокумента1С.ТабличныеЧасти[ТекущаяСтрока.ГлавнаяТаблица].Реквизиты.Скопировать();
	
КонецПроцедуры

Процедура СвязиТабличныхЧастейПолеПодчиненнойТаблицыНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.СвязиТабличныхЧастей.ТекущаяСтрока;	
	ЭлементыФормы.СвязиТабличныхЧастей.Колонки.ПолеПодчиненнойТаблицы.ЭлементУправления.СписокВыбора = СтруктураДокумента1С.ТабличныеЧасти[ТекущаяСтрока.ПодчиненнаяТаблица].Реквизиты.Скопировать();
	
КонецПроцедуры

Процедура СвязиТабличныхЧастейПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
		
	НоваяСтрока = СвязиТабличныхЧастей.Добавить();	
	НоваяСтрока.ТабличнаяЧастьДокументаMS = ТабличнаяЧастьДокументаMS;	
	
	Если Копирование Тогда
		
		ТекущаяСтрока = ЭлементыФормы.СвязиТабличныхЧастей.ТекущаяСтрока;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);		
		
	КонецЕсли; 
	
	ЭлементыФормы.СвязиТабличныхЧастей.ТекущаяСтрока = НоваяСтрока;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокТабличныхЧастей1С()
	
	СписокТЧ = Новый СписокЗначений;
	Для Каждого ЭлементТЧ Из СтруктураДокумента1С.ТабличныеЧасти Цикл
		СписокТЧ.Добавить(ЭлементТЧ.Значение.Имя, ЭлементТЧ.Значение.Синоним);
	КонецЦикла;
	Если СписокТЧ.НайтиПоЗначению("(Запрос)") = Неопределено Тогда
		СписокТЧ.Добавить("(Запрос)", "(Запрос)");
	КонецЕсли;
	СписокТЧ.СортироватьПоПредставлению();
	
	Возврат СписокТЧ;
	
КонецФункции

Функция ПолучитьСписокТабличныхЧастейMS()
	
	СписокТЧ = Новый СписокЗначений;
	
	СписокТЧ.Добавить("СтрокиФакт");
	СписокТЧ.Добавить("СтрокиПлан");
	СписокТЧ.Добавить("СерииПлан");
	СписокТЧ.Добавить("СерииФакт");
	СписокТЧ.Добавить("СерийныеНомераПлан");
	СписокТЧ.Добавить("СерийныеНомераФакт");
		
	СписокДопТаблиц = _ЛокКонтекст.СтруктураМетаданных.ДокументыMS[ТипДокументаMS].ДополнительныеТаблицы;
	
	Для Каждого ДопТаблица Из СписокДопТаблиц Цикл		
		СписокТЧ.Добавить(ДопТаблица.Представление);		
	КонецЦикла;	
	
	Возврат СписокТЧ;
	
КонецФункции	

#КонецОбласти
