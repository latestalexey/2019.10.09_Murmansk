
Процедура ПриОткрытии()
	
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СтрокиФакт");
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СтрокиПлан");
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СерииПлан");
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СерииФакт");
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СерийныеНомераПлан");
	ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить("СерийныеНомераФакт");
	
	// + ZHKN 06.09.2017
	СписокДопТаблиц = _ЛокКонтекст.СтруктураМетаданных.ДокументыMS[ТипДокументаMS].ДополнительныеТаблицы;
	
	Для Каждого ДопТаблица Из СписокДопТаблиц Цикл		
		ЭлементыФормы.ТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора.Добавить(ДопТаблица.Представление);		
	КонецЦикла;
	// - ZHKN 06.09.2017
	
	СписокТЧ = Новый СписокЗначений;
	Для Каждого ЭлементТЧ Из СтруктураДокумента1С.ТабличныеЧасти Цикл
		СписокТЧ.Добавить(ЭлементТЧ.Значение.Имя, ЭлементТЧ.Значение.Синоним);
	КонецЦикла;
	СписокТЧ.СортироватьПоПредставлению();
	ЭлементыФормы.ТабличныеЧасти.Колонки.НастройкаТЧСтрокой.ЭлементУправления.СписокВыбора = СписокТЧ;	
	// + ZHKN 03.10.2017
	Для Каждого СтрокаТЧ Из ЭлементыФормы.ТабличныеЧасти.Значение Цикл
		Если СтрокаТЧ.ОсновнаяТабличнаяЧасть Тогда
			ОсновнаяТЧ = СтрокаТЧ;
			Прервать;	
		КонецЕсли;
	КонецЦикла;
	// - ZHKN 03.10.2017
КонецПроцедуры

//Обработчики команд формы
Процедура ОсновныеДействияСохранитьИЗакрыть(Кнопка)
	
	Результат = ТабличныеЧасти.Скопировать();
	Закрыть(Результат);
	
КонецПроцедуры

Процедура Отменить(Команда)

	Закрыть(Неопределено);
	
КонецПроцедуры

Процедура ТабличныеЧастиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)		
	
	Если ВРег(СокрЛП(Колонка.Имя)) <> ВРег(СокрЛП("Настроить")) Тогда		
		Возврат;			
	КонецЕсли; 
	
	ТекущаяСтрока = ЭлементыФормы.ТабличныеЧасти.ТекущаяСтрока;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТабличнаяЧастьДокументаMS) Тогда			
		Предупреждение("Не выбрана табличная часть документа Mobile SMARTS"); 
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(ТекущаяСтрока.НастройкаТЧСтрокой) Тогда			
		Предупреждение("Не выбрана табличная часть документа 1С"); 
		Возврат;	
	КонецЕсли; 	
	
	ФормаРедактирования = ПолучитьФорму("ФормаНастройки_ПравилаЗагрузкиПолей", ЭтаФорма, ЭтаФорма);
	ФормаРедактирования._ЛокКонтекст  = _ЛокКонтекст;
	ФормаРедактирования.КлючНастройки = "Загрузка_ТабличнаяЧастьДокумента";
	ФормаРедактирования.КонтрольКоличества               = КонтрольКоличества;
	ФормаРедактирования.ТипДокумента1С                   = ТипДокумента1С;
	ФормаРедактирования.ТипДокументаMS                   = ТипДокументаMS;
	ФормаРедактирования.ИмяТабличнойЧастиMS              = ТекущаяСтрока.ТабличнаяЧастьДокументаMS;
	ФормаРедактирования.ИмяТабличнойЧасти1С              = ТекущаяСтрока.НастройкаТЧСтрокой;
	// + ZHKN 03.10.2017
	СписокТаблицДляСвязиПоКлючу = Новый СписокЗначений;  
	        
	Если ТекущаяСтрока.ОсновнаяТабличнаяЧасть И ТабличныеЧасти.Количество() > 1 Тогда     
		Для Каждого СтрокаТЧ Из ТабличныеЧасти Цикл
			Если СтрокаТЧ.СопоставлятьСОсновнойТЧ Тогда
				СписокТаблицДляСвязиПоКлючу.Добавить(СтрокаТЧ.ТабличнаяЧастьДокумента1С, СтрокаТЧ.НастройкаТЧСтрокой);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ФормаРедактирования.СписокТаблицДляСвязиПоКлючу = СписокТаблицДляСвязиПоКлючу;
	// - ZHKN 03.10.2017
	Если ТипЗнч(ТекущаяСтрока.ТабличнаяЧастьДокумента1С) = Тип("ТаблицаЗначений") Тогда
		ФормаРедактирования.ТаблицаРеквизитовИсходная   = ТекущаяСтрока.ТабличнаяЧастьДокумента1С.Скопировать();
	КонецЕсли;
	ФормаРедактирования.ТаблицаРеквизитовДополнительная  = ТаблицаРеквизитовДополнительная;
	
	ФормаРедактирования.Обработчик_ПередЗаполнениемТабличнойЧасти = ТекущаяСтрока.Обработчик_ПередЗаполнениемТабличнойЧасти;
	ФормаРедактирования.Обработчик_ПередЗаполнениемСтрокиТабличнойЧасти = ТекущаяСтрока.Обработчик_ПередЗаполнениемСтрокиТабличнойЧасти;
	ФормаРедактирования.Обработчик_ПослеЗаполненияСтрокиТабличнойЧасти = ТекущаяСтрока.Обработчик_ПослеЗаполненияСтрокиТабличнойЧасти;
	ФормаРедактирования.Обработчик_ПослеЗаполненияТабличнойЧасти = ТекущаяСтрока.Обработчик_ПослеЗаполненияТабличнойЧасти;
	
	РезультатРедактирования = ФормаРедактирования.ОткрытьМодально();;
	
	Если РезультатРедактирования <> Неопределено Тогда
		ТекущаяСтрока.ТабличнаяЧастьДокумента1С = РезультатРедактирования.ТаблицаНастроек;
		ТекущаяСтрока.Обработчик_ПередЗаполнениемТабличнойЧасти = РезультатРедактирования.Обработчик_ПередЗаполнениемТабличнойЧасти;
		ТекущаяСтрока.Обработчик_ПередЗаполнениемСтрокиТабличнойЧасти = РезультатРедактирования.Обработчик_ПередЗаполнениемСтрокиТабличнойЧасти;
		ТекущаяСтрока.Обработчик_ПослеЗаполненияСтрокиТабличнойЧасти = РезультатРедактирования.Обработчик_ПослеЗаполненияСтрокиТабличнойЧасти;
		ТекущаяСтрока.Обработчик_ПослеЗаполненияТабличнойЧасти = РезультатРедактирования.Обработчик_ПослеЗаполненияТабличнойЧасти;
	КонецЕсли;
	
КонецПроцедуры
// + ZHKN 03.10.2017
Процедура ТабличныеЧастиПриИзмененииФлажка(Элемент, Колонка)
	
	ОсновнаяТабличнаяЧасть = Элемент.ТекущаяСтрока.ОсновнаяТабличнаяЧасть;
	СопоставлятьСОсновнойТЧ = Элемент.ТекущаяСтрока.СопоставлятьСОсновнойТЧ;
	
	Если Колонка.Имя = "ОсновнаяТабличнаяЧасть" Тогда
		Если ОсновнаяТабличнаяЧасть Тогда
			Если ЗначениеЗаполнено(ОсновнаяТЧ) Тогда
				ОсновнаяТЧ.ОсновнаяТабличнаяЧасть = Ложь;	
			КонецЕсли;
			Элемент.ТекущаяСтрока.СопоставлятьСОсновнойТЧ = Ложь;
			ОсновнаяТЧ = Элемент.ТекущаяСтрока;
		Иначе
			ОсновнаяТЧ = Неопределено;
			Для Каждого СтрокаТЧ Из ЭлементыФормы.ТабличныеЧасти.Значение Цикл
				СтрокаТЧ.СопоставлятьСОсновнойТЧ = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
		
	Если Колонка.Имя = "СопоставлятьСОсновнойТЧ" И СопоставлятьСОсновнойТЧ Тогда
		Если ОсновнаяТабличнаяЧасть Или Не ЗначениеЗаполнено(ОсновнаяТЧ) Тогда	
			Элемент.ТекущаяСтрока.СопоставлятьСОсновнойТЧ = Ложь;
		КонецЕсли;
	КонецЕсли;		
	
КонецПроцедуры
// - ZHKN 03.10.2017

Процедура ТабличныеЧастиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	флНастроено = (ДанныеСтроки.ТабличнаяЧастьДокумента1С.Количество() > 0);
	
	Если флНастроено Тогда
		ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроено";
	Иначе		
		ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроить";			
	КонецЕсли;
	ОформлениеСтроки.Ячейки.Настроить.ЦветТекста = ?(флНастроено, WebЦвета.Зеленый, WebЦвета.Красный);	
	
КонецПроцедуры
