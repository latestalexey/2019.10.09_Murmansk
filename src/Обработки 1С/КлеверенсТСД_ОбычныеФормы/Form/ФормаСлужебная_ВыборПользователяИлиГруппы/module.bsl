
#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()	
	
	Если ТекущиеПользователи <> "" И ВРег(СокрЛП(ТекущиеПользователи)) <> ВРег(СокрЛП("Никому конкретно")) Тогда		 
		МассивПользователейMS = _ЛокКонтекст.ЛокЯдро_РазложитьСтрокуВМассивПодстрок(ТекущиеПользователи, "|");
		
		Для каждого Элемент Из МассивПользователейMS Цикл
			НайденнаяСтрока = ПользователиMS.Строки.Найти(Элемент, "ИдГруппыПользователя", Истина);
			Если НайденнаяСтрока <> Неопределено Тогда
				НайденнаяСтрока.Выгружать = Истина;
			КонецЕсли;
		КонецЦикла; 			
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область ОбработчикиСобытийЭлементовФормы_ОсновныеДействия

Процедура ОсновныеДействияВыбрать(Кнопка)
	
	МассивПользователейMS = Новый Массив;
	СформироватьМассивПользователейMS(ПользователиMS.Строки, МассивПользователейMS);
	Закрыть(МассивПользователейMS);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_КоманднаяПанель_ПользователиMS

Процедура КоманднаяПанельПользователиMSВыделитьВсе(Кнопка)
	
	СтрокиИсключения = Новый Массив;
	СтрокиИсключения.Добавить("Выгружать всем");
	СтрокиИсключения.Добавить("Спрашивать при выгрузке");	
	ИзменитьФлажки(Истина, Неопределено, СтрокиИсключения);	
	РазвернутьДерево(ЭлементыФормы.ПользователиMS);
	
КонецПроцедуры

Процедура КоманднаяПанельПользователиMSОчиститьВыделение(Кнопка)
	
	ИзменитьФлажки(Ложь);	
	РазвернутьДерево(ЭлементыФормы.ПользователиMS);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_Таблица_ПользователиMS

Процедура ПользователиMSПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ПользователиMSПередНачаломИзменения(Элемент, Отказ)
	
	Если ВРег(СокрЛП(Элемент.ТекущаяКолонка.Имя)) <> ВРег(СокрЛП("Выгружать")) Тогда	
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПользователиMSПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ПользователиMSПриИзмененииФлажка(Элемент, Колонка)
	
	Если ВРег(СокрЛП(Колонка.Имя)) <> ВРег(СокрЛП("Выгружать")) Тогда	
		Возврат;	
	КонецЕсли; 
	
	ТекДанные = Элемент.ТекущиеДанные;		
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 	
	
	Если ТекДанные.Выгружать Тогда		
		
		Если Найти(ВРег(СокрЛП("Выгружать всем/Спрашивать при выгрузке")), ВРег(СокрЛП(ТекДанные.ИдГруппыПользователя))) > 0 Тогда			
			СтрокиИсключения = Новый Массив;
			СтрокиИсключения.Добавить(ТекДанные.ИдГруппыПользователя);		
			СтрокиДляИзменения = Неопределено;
		Иначе
			СтрокиДляИзменения = Новый Массив;
			СтрокиДляИзменения.Добавить("Выгружать всем");
			СтрокиДляИзменения.Добавить("Спрашивать при выгрузке");
			СтрокиИсключения = Неопределено;
		КонецЕсли;	
		
		ИзменитьФлажки(Ложь, СтрокиДляИзменения, СтрокиИсключения);			
		РазвернутьДерево(ЭлементыФормы.ПользователиMS);		
		
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#Область ВспомогательныеМеханизмы

Процедура ИзменитьФлажки(ЗначениеФлажка, СтрокиДляИзменения = Неопределено, СтрокиИсключения = Неопределено)		
	
	УстановитьЗначениеФлажкаВСтрокахДерева(ПользователиMS.Строки, ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);			
	
КонецПроцедуры

Процедура УстановитьЗначениеФлажкаВСтрокахДерева(СтрокиДерева, ЗначениеФлажка, СтрокиДляИзменения = Неопределено, СтрокиИсключения = Неопределено)
	
	Для каждого Строка Из СтрокиДерева Цикл
		
		Если СтрокиИсключения <> Неопределено И СтрокиИсключения.Найти(Строка.ИдГруппыПользователя) <> Неопределено Тогда
			Продолжить;
		Иначе
			Если СтрокиДляИзменения = Неопределено Тогда
				Строка.Выгружать = ЗначениеФлажка;				
			Иначе
				Если СтрокиДляИзменения.Найти(Строка.ИдГруппыПользователя) <> Неопределено Тогда	
					Строка.Выгружать = ЗначениеФлажка;
				Иначе
					Продолжить;
				КонецЕсли;	
			КонецЕсли;				
		КонецЕсли;	
		
		УстановитьЗначениеФлажкаВСтрокахДерева(Строка.Строки, ЗначениеФлажка, СтрокиДляИзменения, СтрокиИсключения);
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура РазвернутьДерево(ТабличноеПоле)
	
	Строка = ТабличноеПоле.ТекущаяСтрока;
	Если Строка <> Неопределено Тогда
		ТабличноеПоле.Развернуть(Строка, Истина);
	КонецЕсли;
        
КонецПроцедуры

Процедура СформироватьМассивПользователейMS(СтрокиДереваЗначений, МассивПользователейMS)
	
	Для Каждого Строка из СтрокиДереваЗначений Цикл
		
		Если Строка.Выгружать Тогда
			СтруктураПользователя = Новый Структура; 
			СтруктураПользователя.Вставить("Ид", 	Строка.ИдГруппыПользователя);
			СтруктураПользователя.Вставить("Имя", 	Строка.ГруппаПользователь);			
			МассивПользователейMS.Добавить(СтруктураПользователя);			
		КонецЕсли;
		
		СформироватьМассивПользователейMS(Строка.Строки, МассивПользователейMS);
		
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти