#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
	
	ОбновитьТаблицуПроизвольныхКодов();
	
	Если КлючНастройки = "Выгрузка_ШапкаДокумента" Тогда 
		Заголовок = Заголовок + " (Раздел: Выгрузка шапки документа)";
	ИначеЕсли КлючНастройки = "Выгрузка_ТабличнаяЧастьДокумента" Тогда 
		Заголовок = Заголовок + " (Раздел: Выгрузка табличной части документа)";
	ИначеЕсли КлючНастройки = "Загрузка_ШапкаДокумента" Тогда 
		Заголовок = Заголовок + " (Раздел: Загрузка шапки документа)";
	ИначеЕсли КлючНастройки = "Загрузка_ТабличнаяЧастьДокумента" Тогда 
		Заголовок = Заголовок + " (Раздел: Загрузка табличной части документа)";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПанельДействийФормы

Процедура ОсновныеДействияСохранитьВФайл(Кнопка)
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("ТаблицаПроизвольныхКодов", ТаблицаПроизвольныхКодов);
	
	_ЛокКонтекст.ЛокЯдро_СохранитьНастройкиВФайл(СтруктураНастройки, "ПроизвольныеКоды_" + КлючНастройки);
	
КонецПроцедуры

Процедура ОсновныеДействияЗагрузитьИзФайла(Кнопка)
	
	СтруктураНастроек = _ЛокКонтекст.ЛокЯдро_ПолучитьНастройкиИзФайла("ПроизвольныеКоды_" + КлючНастройки);
	
	Если СтруктураНастроек <> Неопределено Тогда
		
		_ЛокКонтекст.ЛокЯдро_ЗаполнитьФормуЗначениямиИзСтруктуры(ЭтаФорма, СтруктураНастроек);
		_ЛокКонтекст.ЛокЯдро_СохранитьНастройкуВБазеSMARTS("ПроизвольныеКоды_" + КлючНастройки, ТаблицаПроизвольныхКодов);
		_ЛокКонтекст.ЛокЯдро_ЗаписатьВКэшНастройкуSMARTS("ПроизвольныеКоды_" + КлючНастройки, ТаблицаПроизвольныхКодов);
		
		ОбновитьТаблицуПроизвольныхКодов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеМеханизмы

Процедура ОткрытьФормуРедактированияНастройки(ЭтоНоваяНастройка, ЭтоКопирование = Ложь)
	
	ФормаРедактирования = ПолучитьФорму("ФормаНастройки_ПроизвольныйКод", ЭтаФорма, ЭтаФорма);
	ФормаРедактирования._ЛокКонтекст = _ЛокКонтекст;
	ФормаРедактирования.ЭтоНоваяНастройка = ЭтоНоваяНастройка;
	ФормаРедактирования.КлючНастройки = КлючНастройки;
	ФормаРедактирования.ТипИсходногоДокумента1С = ТипИсходногоДокумента1С;
	ФормаРедактирования.ИмяТабличнойЧасти1С = ИмяТабличнойЧасти1С;
	ФормаРедактирования.ПравилаОбменаШапка = ПравилаОбменаШапка;
	ФормаРедактирования.ПравилаОбменаТабличнаяЧасть = ПравилаОбменаТабличнаяЧасть;
	                                                                     
	Если НЕ ЭтоНоваяНастройка ИЛИ ЭтоКопирование Тогда
		ТекущаяСтрока = ЭлементыФормы.ТаблицаПроизвольныхКодов.ТекущаяСтрока;
		ЗаполнитьЗначенияСвойств(ФормаРедактирования, ТекущаяСтрока);
	КонецЕсли;
	
	ID_РедактируемойНастройки = ФормаРедактирования.ОткрытьМодально();
	Если ЗначениеЗаполнено(ID_РедактируемойНастройки) Тогда
		ОбновитьТаблицуПроизвольныхКодов(ID_РедактируемойНастройки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьТаблицуПроизвольныхКодов(ID_ТекущейСтроки = "")
	
	ТаблицаПроизвольныхКодов = _ЛокКонтекст.ЛокЯдро_ПолучитьИзКэшаНастройкуSMARTS("ПроизвольныеКоды_" + КлючНастройки);
	
	Если ЗначениеЗаполнено(ID_ТекущейСтроки) Тогда
		НайденнаяСтрока = ЭлементыФормы.ТаблицаПроизвольныхКодов.Значение.Найти(ID_ТекущейСтроки, "ID_ПроизвольногоКода");
		Если НайденнаяСтрока <> Неопределено Тогда
			ЭлементыФормы.ТаблицаПроизвольныхКодов.ТекущаяСтрока = НайденнаяСтрока;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьФормуСВыборомЭлемента()
	
	Если ЗначениеЗаполнено(ЭлементыФормы.ТаблицаПроизвольныхКодов.ТекущаяСтрока) Тогда
		Закрыть(ЭлементыФормы.ТаблицаПроизвольныхКодов.ТекущаяСтрока.ID_ПроизвольногоКода);
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоля

Процедура ТаблицаПроизвольныхКодовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗакрытьФормуСВыборомЭлемента();
КонецПроцедуры

Процедура ТаблицаПроизвольныхКодовПередНачаломДобавления(Элемент, Отказ, Копирование)
	ОткрытьФормуРедактированияНастройки(Истина, Копирование);
	Отказ = Истина;
КонецПроцедуры

Процедура ТаблицаПроизвольныхКодовПередНачаломИзменения(Элемент, Отказ)
	ОткрытьФормуРедактированияНастройки(Ложь);
КонецПроцедуры

Процедура ТаблицаПроизвольныхКодовПередУдалением(Элемент, Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры


#КонецОбласти

Процедура КомПанельТаблицыВыбрать(Кнопка)
	ЗакрытьФормуСВыборомЭлемента();
КонецПроцедуры
