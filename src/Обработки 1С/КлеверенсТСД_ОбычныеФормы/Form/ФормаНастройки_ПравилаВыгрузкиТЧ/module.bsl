
Перем НесколькоТабличныхЧастей;
Перем ТабличнаяЧастьДокументаMSПредыдущееЗначение;
Перем НастройкаТЧСтрокойПредыдущееЗначение;

#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
	
	НесколькоТабличныхЧастей = "Несколько табличных частей";
		
	ЭлементыФормы.ОтображаемыеТабличныеЧасти.Колонки.НастройкаТЧСтрокой.ЭлементУправления.СписокВыбора = ПолучитьСписокТабличныхЧастей1С();		
	
	ЗаполнитьОтображаемыеТабличныеЧасти();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийОсновныеДействия

Процедура ОсновныеДействияСохранитьИЗакрыть(Кнопка)
	
	Результат = Новый Структура;
	Результат.Вставить("ТабличныеЧасти", 		ТабличныеЧасти.Скопировать());	
	Результат.Вставить("СвязиТабличныхЧастей", 	СвязиТабличныхЧастей.Скопировать());	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТабличногоПоля_ОтображаемыеТабличныеЧасти

Процедура ОтображаемыеТабличныеЧастиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВРег(СокрЛП(Колонка.Имя)) <> ВРег(СокрЛП("Настроить")) Тогда		
		Возврат;			
	КонецЕсли; 
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.ТабличнаяЧастьДокументаMS) Тогда			
		Предупреждение("Не выбрана табличная часть документа Mobile SMARTS"); 
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(ТекущаяСтрока.НастройкаТЧСтрокой) Тогда			
		Предупреждение("Не выбрана табличная часть документа 1С"); 
		Возврат;	
	КонецЕсли; 
	
	Если Найти(ВРег(СокрЛП(ТекущаяСтрока.НастройкаТЧСтрокой)), ВРег(СокрЛП(НесколькоТабличныхЧастей))) Тогда
		ФормаРедактирования = ПолучитьФорму("ФормаНастройки_СвязиВыгрузкиТЧ", ЭтаФорма, ЭтаФорма);
		ФормаРедактирования._ЛокКонтекст  				= _ЛокКонтекст;		
		ФормаРедактирования.ТипДокумента1С              = ТипДокумента1С;
		ФормаРедактирования.ТипДокументаMS              = ТипДокументаMS;
		ФормаРедактирования.СтруктураДокумента1С        = СтруктураДокумента1С;
		ФормаРедактирования.СтруктураДокументаMS        = СтруктураДокументаMS;				
		ФормаРедактирования.ТабличнаяЧастьДокументаMS  	= ТекущаяСтрока.ТабличнаяЧастьДокументаMS;		
		ФормаРедактирования.ТабличныеЧасти 				= ТабличныеЧасти.Скопировать(Новый Структура("ТабличнаяЧастьДокументаMS", ТекущаяСтрока.ТабличнаяЧастьДокументаMS));
		ФормаРедактирования.СвязиТабличныхЧастей 		= СвязиТабличныхЧастей.Скопировать(Новый Структура("ТабличнаяЧастьДокументаMS", ТекущаяСтрока.ТабличнаяЧастьДокументаMS));
		
		РезультатРедактирования = ФормаРедактирования.ОткрытьМодально();
		
		Если РезультатРедактирования <> Неопределено Тогда
			
			УдалитьСтрокиПодчиненныхТабличныхЧастей(ТекущаяСтрока.ТабличнаяЧастьДокументаMS);
			
			НастройкаТЧСтрокой = "(" + НесколькоТабличныхЧастей + ": ";			
			
			Для каждого Строка Из РезультатРедактирования.ТабличныеЧасти Цикл
				НоваяСтрока = ТабличныеЧасти.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, , "ТабличнаяЧастьДокумента1С");				
				НоваяСтрока.ТабличнаяЧастьДокумента1С = Строка.ТабличнаяЧастьДокумента1С.Скопировать();						
				НастройкаТЧСтрокой = НастройкаТЧСтрокой + Строка.НастройкаТЧСтрокой + ", ";
			КонецЦикла; 
			
			НастройкаТЧСтрокой = Лев(НастройкаТЧСтрокой, СтрДлина(НастройкаТЧСтрокой) - 2) + ")";						
			
			Если РезультатРедактирования.ТабличныеЧасти.Количество() = 1 Тогда 
				НастройкаТЧСтрокой = РезультатРедактирования.ТабличныеЧасти[0].НастройкаТЧСтрокой;	
			КонецЕсли; 	
			
			ТекущаяСтрока.НастройкаТЧСтрокой 	= НастройкаТЧСтрокой; 			
			ТекущаяСтрока.Настроить				= "Настроено";
			
			Для каждого Строка Из РезультатРедактирования.СвязиТабличныхЧастей Цикл				
				ЗаполнитьЗначенияСвойств(СвязиТабличныхЧастей.Добавить(), Строка);												
			КонецЦикла; 			
			
		КонецЕсли;	
		
	Иначе					
		
		НайдСтрока = ТабличныеЧасти.Найти(ТекущаяСтрока.ТабличнаяЧастьДокументаMS, "ТабличнаяЧастьДокументаMS");
		
		ФормаРедактирования = ПолучитьФорму("ФормаНастройки_ПравилаВыгрузкиПолей", ЭтаФорма, ЭтаФорма);
		ФормаРедактирования._ЛокКонтекст  = _ЛокКонтекст;
		ФормаРедактирования.КлючНастройки = "Выгрузка_ТабличнаяЧастьДокумента";	
		ФормаРедактирования.ТипДокумента1С                   = ТипДокумента1С;
		ФормаРедактирования.ТипДокументаMS                   = ТипДокументаMS;
		ФормаРедактирования.ИмяТабличнойЧастиMS              = ТекущаяСтрока.ТабличнаяЧастьДокументаMS;
		ФормаРедактирования.ИмяТабличнойЧасти1С              = ТекущаяСтрока.НастройкаТЧСтрокой;
		
		Если НайдСтрока <> Неопределено Тогда
			ФормаРедактирования.ТекстЗапросаВыгрузкиНаТСД       = НайдСтрока.ТекстЗапросаВыгрузкиНаТСД;
			ФормаРедактирования.ТекстКодаПолучениеПараметров = НайдСтрока.ТекстКодаПолучениеПараметров;
			Если ТипЗнч(НайдСтрока.ТабличнаяЧастьДокумента1С) = Тип("ТаблицаЗначений") Тогда
				ФормаРедактирования.ТаблицаРеквизитовИсходная	= НайдСтрока.ТабличнаяЧастьДокумента1С.Скопировать();
			КонецЕсли;
		КонецЕсли;	
		
		ФормаРедактирования.ТаблицаРеквизитовДополнительная  = ТаблицаРеквизитовДополнительная;
		
		РезультатРедактирования = ФормаРедактирования.ОткрытьМодально();
		
		Если РезультатРедактирования <> Неопределено Тогда
			
			Если НайдСтрока = Неопределено Тогда
				НайдСтрока = ТабличныеЧасти.Добавить();
				НайдСтрока.НастройкаТЧСтрокой 			= ТекущаяСтрока.НастройкаТЧСтрокой;
				НайдСтрока.ТабличнаяЧастьДокументаMS 	= ТекущаяСтрока.ТабличнаяЧастьДокументаMS;
			КонецЕсли;	
			
			НайдСтрока.ТабличнаяЧастьДокумента1С 	= РезультатРедактирования.ТаблицаНастроек;		
			НайдСтрока.ТекстЗапросаВыгрузкиНаТСД 	= РезультатРедактирования.ТекстЗапросаВыгрузкиНаТСД;		
			НайдСтрока.ТекстКодаПолучениеПараметров = РезультатРедактирования.ТекстКодаПолучениеПараметров;
			НайдСтрока.Настроить					= "Настроено";
			
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ТабличнаяЧастьДокументаMS = ОформлениеСтроки.ДанныеСтроки.ТабличнаяЧастьДокументаMS;
		
		флНастроено = Ложь;
		НайдСтроки = ТабличныеЧасти.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТабличнаяЧастьДокументаMS));
		Если НайдСтроки.Количество() Тогда
			Для каждого НайдСтрока Из НайдСтроки Цикл
				Если НайдСтрока.ТабличнаяЧастьДокумента1С.Количество() Тогда
					флНастроено = Истина;
					Прервать;
				КонецЕсли;	
			КонецЦикла; 		
		КонецЕсли; 				
		
		Если флНастроено Тогда
			ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроено";
		Иначе		
			ОформлениеСтроки.Ячейки.Настроить.Текст = "Настроить";			
		КонецЕсли;
		
		ОформлениеСтроки.Ячейки.Настроить.ЦветТекста = ?(флНастроено, WebЦвета.Зеленый, WebЦвета.Красный);			
	
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
	Для каждого Строка Из ОтображаемыеТабличныеЧасти Цикл
		Если НЕ ЗначениеЗаполнено(Строка.ТабличнаяЧастьДокументаMS) Тогда							
			Предупреждение("Перед добавлением новой строки нужно заполнить табличные части Mobile SMARTS у существующих строк"); 
			Возврат;
		КонецЕсли; 			
	КонецЦикла; 
	
	НоваяСтрока = ОтображаемыеТабличныеЧасти.Добавить();
	
	Если Копирование Тогда		
		
		ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;	
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока, , "ТабличнаяЧастьДокументаMS");		
		
		НайдСтроки = ТабличныеЧасти.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТекущаяСтрока.ТабличнаяЧастьДокументаMS));	
		Для каждого НайдСтрока Из НайдСтроки Цикл
			СтрокаТЧ = ТабличныеЧасти.Добавить();							
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, НайдСтрока, , "ТабличнаяЧастьДокументаMS, ТабличнаяЧастьДокумента1С");		
			СтрокаТЧ.ТабличнаяЧастьДокумента1С = НайдСтрока.ТабличнаяЧастьДокумента1С.Скопировать();		
		КонецЦикла; 	
		
		НайдСтроки = СвязиТабличныхЧастей.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТекущаяСтрока.ТабличнаяЧастьДокументаMS));	
		Для каждого НайдСтрока Из НайдСтроки Цикл				
			ЗаполнитьЗначенияСвойств(СвязиТабличныхЧастей.Добавить(), НайдСтрока, , "ТабличнаяЧастьДокументаMS");		
		КонецЦикла;	
	
	КонецЕсли; 					
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиТабличнаяЧастьДокументаMSНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧастьДокументаMSПредыдущееЗначение = ТекущаяСтрока.ТабличнаяЧастьДокументаMS;	
	
	ЭлементыФормы.ОтображаемыеТабличныеЧасти.Колонки.ТабличнаяЧастьДокументаMS.ЭлементУправления.СписокВыбора = ПолучитьСписокВыбораТабличныхЧастейMS();
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиНастройкаТЧСтрокойНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	НастройкаТЧСтрокойПредыдущееЗначение = ТекущаяСтрока.НастройкаТЧСтрокой;
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	УдалитьСтрокиПодчиненныхТабличныхЧастей(ТекущаяСтрока.ТабличнаяЧастьДокументаMS);
	
	ОтображаемыеТабличныеЧасти.Удалить(ТекущаяСтрока);
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиПередНачаломИзменения(Элемент, Отказ)
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧастьДокументаMSПредыдущееЗначение = ТекущаяСтрока.ТабличнаяЧастьДокументаMS;
	НастройкаТЧСтрокойПредыдущееЗначение		= ТекущаяСтрока.НастройкаТЧСтрокой;
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиТабличнаяЧастьДокументаMSПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТабличнаяЧастьДокументаMSПредыдущееЗначение = ТекущаяСтрока.ТабличнаяЧастьДокументаMS Тогда
		Возврат;			
	КонецЕсли;	
	
	ИзменитьВладельцаПодчиненныхТабличныхЧастей(ТабличнаяЧастьДокументаMSПредыдущееЗначение, ТекущаяСтрока.ТабличнаяЧастьДокументаMS);
	
	ТабличнаяЧастьДокументаMSПредыдущееЗначение = ТекущаяСтрока.ТабличнаяЧастьДокументаMS;
	
КонецПроцедуры

Процедура ОтображаемыеТабличныеЧастиНастройкаТЧСтрокойПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.ОтображаемыеТабличныеЧасти.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаТЧСтрокойПредыдущееЗначение = ТекущаяСтрока.НастройкаТЧСтрокой Тогда
		Возврат;			
	КонецЕсли;	
	
	УдалитьСтрокиПодчиненныхТабличныхЧастей(ТекущаяСтрока.ТабличнаяЧастьДокументаMS);
	
	Если НЕ Найти(ВРег(СокрЛП(ТекущаяСтрока.НастройкаТЧСтрокой)), ВРег(СокрЛП(НесколькоТабличныхЧастей))) Тогда
		ЗаполнитьЗначенияСвойств(ТабличныеЧасти.Добавить(), ТекущаяСтрока);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСписокТабличныхЧастей1С()
	
	СписокТЧ = Новый СписокЗначений;
	Для Каждого ЭлементТЧ Из СтруктураДокумента1С.ТабличныеЧасти Цикл
		СписокТЧ.Добавить(ЭлементТЧ.Значение.Имя, ЭлементТЧ.Значение.Синоним);
	КонецЦикла;
	Если СписокТЧ.НайтиПоЗначению("(Запрос)") = Неопределено Тогда
		СписокТЧ.Добавить("(Запрос)", "(Запрос)");
	КонецЕсли;
	Если СписокТЧ.НайтиПоЗначению("(" + НесколькоТабличныхЧастей + ")") = Неопределено Тогда
		СписокТЧ.Добавить("(" + НесколькоТабличныхЧастей + ")", "(" + НесколькоТабличныхЧастей + ")");
	КонецЕсли;	
	СписокТЧ.СортироватьПоПредставлению();
	
	Возврат СписокТЧ;
	
КонецФункции

Функция ПолучитьСписокТабличныхЧастейMS()
	
	СписокТЧ = Новый СписокЗначений;
	
	СписокТЧ.Добавить("СтрокиФакт");
	СписокТЧ.Добавить("СтрокиПлан");
	СписокТЧ.Добавить("СерииПлан");
	СписокТЧ.Добавить("СерииФакт");
	СписокТЧ.Добавить("СерийныеНомераПлан");
	СписокТЧ.Добавить("СерийныеНомераФакт");
		
	СписокДопТаблиц = _ЛокКонтекст.СтруктураМетаданных.ДокументыMS[ТипДокументаMS].ДополнительныеТаблицы;
	
	Для Каждого ДопТаблица Из СписокДопТаблиц Цикл		
		СписокТЧ.Добавить(ДопТаблица.Представление);		
	КонецЦикла;	
	
	Возврат СписокТЧ;
	
КонецФункции	

Процедура ЗаполнитьОтображаемыеТабличныеЧасти()
	
	СписокТабличныхЧастей1С = ЭлементыФормы.ОтображаемыеТабличныеЧасти.Колонки.НастройкаТЧСтрокой.ЭлементУправления.СписокВыбора;
	ТЗ = ТабличныеЧасти.Скопировать();
	                       
	КоличествоСтрок = ТабличныеЧасти.Количество();
	Для каждого Строка Из ТабличныеЧасти Цикл		
		НайдСтроки = ТЗ.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", Строка.ТабличнаяЧастьДокументаMS)); 
		Если НЕ НайдСтроки.Количество() Тогда
			Продолжить;
		ИначеЕсли НайдСтроки.Количество() = 1 Тогда
			НайдСтрока = НайдСтроки[0];
			ЗаполнитьЗначенияСвойств(ОтображаемыеТабличныеЧасти.Добавить(), НайдСтрока);
			ТЗ.Удалить(НайдСтрока);
		Иначе
			НастройкаТЧСтрокой = "(" + НесколькоТабличныхЧастей + ": ";
			Для каждого НайдСтрока Из НайдСтроки Цикл
				НастройкаТЧСтрокой = НастройкаТЧСтрокой + НайдСтрока.НастройкаТЧСтрокой + ", ";														
				ТЗ.Удалить(НайдСтрока);
			КонецЦикла; 
			НастройкаТЧСтрокой = Лев(НастройкаТЧСтрокой, СтрДлина(НастройкаТЧСтрокой) - 2) + ")";			
			НоваяСтрока = ОтображаемыеТабличныеЧасти.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.НастройкаТЧСтрокой = НастройкаТЧСтрокой;
			Если СписокТабличныхЧастей1С.НайтиПоЗначению(НастройкаТЧСтрокой) = Неопределено Тогда
				СписокТабличныхЧастей1С.Добавить(НастройкаТЧСтрокой, НастройкаТЧСтрокой);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла; 		
	
	СписокТабличныхЧастей1С.СортироватьПоПредставлению();
	
КонецПроцедуры	

Функция ПолучитьСписокВыбораТабличныхЧастейMS()
	
	СписокТабличныхЧастейMS = ПолучитьСписокТабличныхЧастейMS();
	
	Для Каждого Строка Из ОтображаемыеТабличныеЧасти Цикл
		НайденныйЭлемент = СписокТабличныхЧастейMS.НайтиПоЗначению(Строка.ТабличнаяЧастьДокументаMS);
		Если НайденныйЭлемент <> Неопределено Тогда
			СписокТабличныхЧастейMS.Удалить(НайденныйЭлемент);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокТабличныхЧастейMS;
	
КонецФункции

Процедура УдалитьСтрокиПодчиненныхТабличныхЧастей(ТабличнаяЧастьДокументаMS)

	НайдСтроки = ТабличныеЧасти.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТабличнаяЧастьДокументаMS));
	Для каждого НайдСтрока Из НайдСтроки Цикл
		ТабличныеЧасти.Удалить(НайдСтрока);							
	КонецЦикла; 
	
	НайдСтроки = СвязиТабличныхЧастей.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТабличнаяЧастьДокументаMS));
	Для каждого НайдСтрока Из НайдСтроки Цикл
		СвязиТабличныхЧастей.Удалить(НайдСтрока);							
	КонецЦикла;			

КонецПроцедуры

Процедура ИзменитьВладельцаПодчиненныхТабличныхЧастей(ТабличнаяЧастьДокументаMSПредыдущееЗначение, ТабличнаяЧастьДокументаMS);

	НайдСтроки = ТабличныеЧасти.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТабличнаяЧастьДокументаMSПредыдущееЗначение));
	Для каждого НайдСтрока Из НайдСтроки Цикл
		НайдСтрока.ТабличнаяЧастьДокументаMS = ТабличнаяЧастьДокументаMS;
	КонецЦикла; 
	
	НайдСтроки = СвязиТабличныхЧастей.НайтиСтроки(Новый Структура("ТабличнаяЧастьДокументаMS", ТабличнаяЧастьДокументаMSПредыдущееЗначение));
	Для каждого НайдСтрока Из НайдСтроки Цикл
		НайдСтрока.ТабличнаяЧастьДокументаMS = ТабличнаяЧастьДокументаMS;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти 