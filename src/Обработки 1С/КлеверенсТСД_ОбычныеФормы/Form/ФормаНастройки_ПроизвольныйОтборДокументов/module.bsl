
#Область ОписаниеПеременных 

Перем РезультатРедактирования;

#КонецОбласти

#Область ОбработчикиСобытийФормы

Процедура ПриОткрытии()
	
	Если ЭтоНоваяНастройка Тогда
		ID_ПроизвольногоОтбора = _ЛокКонтекст.ЛокЯдро_ПолучитьНовыйID("CF");
		Наименование = "НОВЫЙ ПРОИЗВОЛЬНЫЙ ОТБОР ДОКУМЕНТОВ";
		ЕстьИзмененияНаФорме = Истина;
	Иначе
		ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.УстановитьТекст(ТекстПроизвольногоОтбора);
		ЕстьИзмененияНаФорме = Ложь;
	КонецЕсли;	
	
	УстановитьПодсказкуПроизвольногоОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_ОсновныеДействия

Процедура ОсновныеДействияСохранить(Кнопка)
	СохранитьНастройку();
КонецПроцедуры

Процедура ОсновныеДействияСохранитьИЗакрыть(Кнопка)
	
	СохранитьНастройку();
	Закрыть(РезультатРедактирования);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_КоманднаяПанельПроизвольногоОтбора

Процедура КоманднаяПанельПроизвольногоОтбораЗакомментировать(Кнопка)
	
	Перем в1,в2,в3,в4;		
	
	ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьГраницыВыделения(в1,в2,в3,в4);

	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.УстановитьТекст(ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьТекст());
			
	сч = в1;
	Пока сч <= в3 Цикл		
		ТекстСтроки = ТекстДок.ПолучитьСтроку(сч);
		ТекстДок.ЗаменитьСтроку(сч,"//"+ТекстСтроки);		
		сч = сч + 1;
	КонецЦикла;
	
	ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.УстановитьТекст(ТекстДок.ПолучитьТекст());	
	
КонецПроцедуры

Процедура КоманднаяПанельПроизвольногоОтбораСнятьКомментарий(Кнопка)
	
	Перем в1,в2,в3,в4;
	
	ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьГраницыВыделения(в1,в2,в3,в4);
	
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.УстановитьТекст(ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьТекст());
	
	сч = в1;
	Пока сч <= в3 Цикл
		ТекстСтроки = ТекстДок.ПолучитьСтроку(сч);
		 Если Лев(СокрЛП(ТекстСтроки), 2) = "//" Тогда
			ТекстСтроки = Прав(ТекстСтроки,СтрДлина(ТекстСтроки)-2);
			ТекстДок.ЗаменитьСтроку(сч,ТекстСтроки);		
		КонецЕсли;
		сч = сч + 1;
	КонецЦикла;
	
	ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.УстановитьТекст(ТекстДок.ПолучитьТекст());
	
КонецПроцедуры

Процедура КоманднаяПанельПроизвольногоОтбораПроверка(Кнопка)
	
	Попытка
		Выполнить(ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьТекст()); 					
		Результат = "Ошибок не найдено!";		
	Исключение
		Результат = ОписаниеОшибки();
	КонецПопытки;
	
	Сообщить(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПодсказкуПроизвольногоОтбора()

	Подсказка = "Результат - возвращаемый результат типа ""Запрос""
	|
	|ТекстЗапроса = """";
	|Результат = Новый Запрос(ТекстЗапроса);";
	
	ЭлементыФормы.Подсказка.УстановитьТекст(Подсказка);	

КонецПроцедуры

Процедура СохранитьНастройку()
	
	СтруктураНастройки = Новый Структура;
	СтруктураНастройки.Вставить("ID_ПроизвольногоОтбора", 	ID_ПроизвольногоОтбора);
	СтруктураНастройки.Вставить("Наименование",           	Наименование);
	СтруктураНастройки.Вставить("ТекстПроизвольногоОтбора", СокрЛП(ЭлементыФормы.ПолеРедактированияТекстаПроизвольногоОтбора.ПолучитьТекст()));
	СтруктураНастройки.Вставить("Описание",               	Описание);
	
	Отбор = Новый Структура("ID_ПроизвольногоОтбора", ID_ПроизвольногоОтбора);
	_ЛокКонтекст.ЛокЯдро_СохранитьСтрокуВТабличнойНастройкеMS("ПроизвольныеОтборыДокументов", Отбор, СтруктураНастройки);
	
	РезультатРедактирования = ID_ПроизвольногоОтбора;
	ЕстьИзмененияНаФорме = Ложь;
	
КонецПроцедуры

#КонецОбласти